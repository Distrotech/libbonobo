AC_INIT(message-of-doom)

AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE(libbonobo2, 1.96.0)

AM_MAINTAINER_MODE

if test -z "$CERTIFIED_GNOMIE"; then
  cat $srcdir/message-of-doom
  exit 1
fi

dnl
dnl Due to the sed scripts being split on 90 line
dnl blocks, this macro needs to be right at the beggining.
dnl
AM_PROG_XML_I18N_TOOLS

GNOME_COMMON_INIT
GNOME_PLATFORM_GNOME_2(yes, force)

AC_ISC_POSIX
AC_PROG_CC
AC_STDC_HEADERS
AC_ARG_PROGRAM
AM_PROG_LIBTOOL
AM_PROG_LEX
AC_PROG_YACC
AC_PATH_PROGS(PATH_TO_XRDB, "xrdb")

dnl utility conditional
AM_CONDITIONAL(FALSE, test "x" = "y")

ALL_LINGUAS="az es fr it nn no sk sv ta"
AM_GNOME2_GETTEXT

AC_SUBST(CFLAGS)
AC_SUBST(CPPFLAGS)
AC_SUBST(LDFLAGS)

GNOME_COMPILE_WARNINGS(maximum)

dnl Test for dladdr
AC_MSG_CHECKING(for dladdr)
have_dladdr=false
AC_TRY_COMPILE([
#define _GNU_SOURCE
#include <dlfcn.h>], [
{
	Dl_info info;
	int     a;
	a = dladdr ("SHLIB_ID", &info);
}], have_dladdr=true)

if $have_dladdr; then
	AC_MSG_RESULT(found)
else
	AC_MSG_RESULT(not found - no auto shlib factory unloads)
fi

AC_DEFINE(HAVE_DLADDR,$have_dladdr)

dnl
dnl Start of pkg-config checks
dnl
GNOME_PKGCONFIG_CHECK_MODULES(ORBIT, ORBit-2.0:2.3.90 ORBit-CosNaming-2.0:2.3.90)
AC_PATH_PROG(ORBIT_IDL, orbit-idl, no)
AC_SUBST(ORBIT_CFLAGS)
AC_SUBST(ORBIT_LIBS)

AC_ARG_ENABLE(orbit2, [  --enable-orbit2        enable ORBit2 bits], enable_orbit2=yes, enable_orbit2=no)
if test z$enable_orbit2 = zyes; then
	AC_DEFINE(ENABLE_ORBIT2)
fi

GNOME_PKGCONFIG_CHECK_MODULES(GLIB, glib-2.0:1.3.1 gobject-2.0:1.3.1 gthread-2.0:1.3.1)
AC_SUBST(GLIB_CFLAGS)
AC_SUBST(GLIB_LIBS)

GNOME_PKGCONFIG_CHECK_MODULES(OAF, oaf-2.0)
AC_SUBST(OAF_CFLAGS)
AC_SUBST(OAF_LIBS)

dnl OAF idl files
OAF_IDL_DIR="`$PKG_CONFIG --variable=datadir oaf-2.0`/idl"
AC_SUBST(OAF_IDL_DIR)

AC_OUTPUT([
Makefile
po/Makefile.in
intl/Makefile
idl/Makefile
bonobo/Makefile
bonobo/libbonobo-2.0.pc
monikers/Makefile
tests/Makefile
])

cat $srcdir/message-of-doom
