AC_PREREQ(2.52)
AC_INIT(message-of-doom)

AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE(libbonobo2, 1.101.2)

AM_MAINTAINER_MODE

if test -z "$CERTIFIED_GNOMIE"; then
  cat $srcdir/message-of-doom
  exit 1
fi

dnl
dnl Due to the sed scripts being split on 90 line
dnl blocks, this macro needs to be right at the beggining.
dnl
AM_PROG_XML_I18N_TOOLS

GNOME_COMMON_INIT
GNOME_PLATFORM_GNOME_2(yes, force)

AC_ISC_POSIX
AC_PROG_CC
AC_STDC_HEADERS
AM_PROG_LIBTOOL
AC_PROG_YACC
AC_PATH_PROGS(PATH_TO_XRDB, "xrdb")

dnl utility conditional
AM_CONDITIONAL(FALSE, test "x" = "y")

ALL_LINGUAS="az es fr it nn no sk sv ta tr zh_TW.Big5"
AM_GLIB_GNU_GETTEXT

AC_SUBST(CFLAGS)
AC_SUBST(CPPFLAGS)
AC_SUBST(LDFLAGS)

GNOME_COMPILE_WARNINGS(maximum)

dnl Test for dladdr
AC_MSG_CHECKING(for dladdr)
have_dladdr=false
AC_TRY_COMPILE([
#define _GNU_SOURCE
#include <dlfcn.h>], [
{
	Dl_info info;
	int     a;
	a = dladdr ("SHLIB_ID", &info);
}], have_dladdr=true)

if $have_dladdr; then
	AC_MSG_RESULT(found)
else
	AC_MSG_RESULT(not found - no auto shlib factory unloads)
fi

AC_DEFINE(HAVE_DLADDR,$have_dladdr)

#
# Start of pkg-config checks
#
PKG_CHECK_MODULES(LIBBONOBO, ORBit-2.0 >= 2.3.93 \
		  ORBit-CosNaming-2.0 >= 2.3.91 glib-2.0 >= 1.3.1 \
		  gobject-2.0 >= 1.3.1 gthread-2.0 >= 1.3.1 \
		  bonobo-activation-2.0 >= 0.9.1)
AC_SUBST(LIBBONOBO_CFLAGS)
AC_SUBST(LIBBONOBO_LIBS)

# Always define this for the moment
AC_DEFINE(ENABLE_ORBIT2)

# orbit-idl
ORBIT_IDL="`$PKG_CONFIG --variable=orbit_idl ORBit-2.0`"
AC_SUBST(ORBIT_IDL)

# Activation idl files
BONOBO_ACTIVATION_IDL_DIR="`$PKG_CONFIG --variable=idldir bonobo-activation-2.0`"
AC_SUBST(BONOBO_ACTIVATION_IDL_DIR)

AC_OUTPUT([
Makefile
po/Makefile.in
idl/Makefile
bonobo/Makefile
bonobo/libbonobo-2.0.pc
monikers/Makefile
samples/Makefile
samples/echo/Makefile
tests/Makefile
])

cat $srcdir/message-of-doom
